plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

application {
    mainClass = project.hasProperty('mainClass') ? project.getProperty('mainClass') : 'com.example.ratelimit.client.SingleKeyBenchmark'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.grpc:grpc-protobuf:1.62.2'
    implementation 'io.grpc:grpc-stub:1.62.2'
    implementation 'org.hdrhistogram:HdrHistogram:2.1.12'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.62.2'
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java', 'build/generated/source/proto/main/grpc'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.62.2'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('generateProto')
}
